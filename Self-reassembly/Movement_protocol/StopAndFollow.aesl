var speed = 50
var maxdistance = 3000

var counter = 0
var countermax = 10
var backprotocol = 0

var following = 0
var stopped = 0

var id = 5

motor.left.target = speed
motor.right.target = speed

onevent prox
	if prox.horizontal[0] < maxdistance and prox.horizontal[1] < maxdistance and prox.horizontal[2] < maxdistance and prox.horizontal[3] < maxdistance and prox.horizontal[4] < maxdistance and prox.horizontal[5] < maxdistance and prox.horizontal[6] < maxdistance then
		following = 0
		call prox.comm.enable(0)
	else
		call prox.comm.enable(1)
		prox.comm.tx = id
	end
	if stopped != 1 and following == 1 then
		call leds.top(32,10,0)
		if prox.horizontal[1] < prox.horizontal[3] + 20 and prox.horizontal[1] > prox.horizontal[3] - 20 then
			motor.left.target = speed
			motor.right.target = speed
		elseif prox.horizontal[1] < prox.horizontal[3] then
			motor.left.target = 0
			motor.right.target = speed
		else
			motor.left.target = speed
			motor.right.target = 0
		end
	elseif stopped != 1 then
		if backprotocol == 0 then
			if prox.horizontal[1] > maxdistance and prox.horizontal[3]> maxdistance then
				backprotocol = 1
				motor.left.target = -speed
				motor.right.target = -speed
			elseif prox.horizontal[2] > maxdistance or prox.horizontal[1] > maxdistance then
				motor.left.target = speed + 100
				motor.right.target = 0
				call leds.top(32,0,0)
			elseif  prox.horizontal[3] > maxdistance then
				motor.left.target = 0
				motor.right.target = speed + 100
				call leds.top(32,32,0)
			elseif prox.horizontal[0] > maxdistance then
				motor.left.target = speed
				motor.right.target = 0
				call leds.top(32,0,32)
			elseif  prox.horizontal[4] > maxdistance then
				motor.left.target = 0
				motor.right.target = speed
				call leds.top(0,32,0)
			else
				motor.left.target = speed
				motor.right.target = speed
				call leds.top(0,32,32)
			end
		else
			call leds.top(0,0,32)
			counter++
			if counter < countermax and prox.horizontal[5] < 3300 and prox.horizontal[6] < 3300 then
				motor.left.target = -speed
				motor.right.target = -speed
			elseif counter >= countermax and counter < (countermax +5) then
				motor.left.target = speed
				motor.right.target = 0
			elseif counter >=  (countermax + 5) then
				motor.left.target = speed
				motor.right.target = speed
				backprotocol = 0
				counter = 0
			end
		end
	end

	
onevent prox.comm
	if prox.comm.rx != 0 and following != 1 then
		motor.left.target = 0
		motor.right.target = 0
		stopped = 1
		timer.period[0] = 200
		if prox.comm.rx == id +1 then
			following = 1
		end
	end
	
onevent timer0
	stopped = 0
	timer.period[0] = 0